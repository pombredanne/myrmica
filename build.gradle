buildscript {
  repositories { jcenter() }
  dependencies { classpath "com.github.ben-manes:gradle-versions-plugin:0.20.0" }
}

plugins { id "com.jfrog.bintray" version "1.8.4" }

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'java'
apply plugin: 'maven-publish'

repositories { jcenter() }

group 'io.vacco.myrmica'
version '0.1.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def description = 'A lightweight Maven artifact and dependency resolver.'
def website = 'https://github.com/vaccovecrana/myrmica'

dependencies {
  compile "org.slf4j:slf4j-api:1.7.25"
  compile "org.jooq:joox-java-6:1.6.0"
  testCompile "org.slf4j:slf4j-simple:1.7.25"
  testCompile "junit:junit:4.12"
  testCompile "io.github.j8spec:j8spec:3.0.0"
}

task sourceJar(type: Jar, dependsOn: classes) { from sourceSets.main.allSource }
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

def pomConfig = {
  licenses {
    license {
      name 'The Apache Software License, Version 2.0'
      url "http://www.apache.org/licenses/LICENSE-2.0.txt"
      distribution "repo"
    }
  }
  developers {
    developer {
      id "jjzazuet"
      name "Jesus Zazueta"
      email "jjzazuet@vaco.io"
    }
  }
  scm { url website }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact sourceJar { classifier "sources" }
      groupId project.group
      artifactId project.name
      version project.version
      pom.withXml {
        def root = asNode()
        root.appendNode('description', description)
        root.appendNode('name', project.name)
        root.appendNode('url', website)
        root.children().last() + pomConfig
      }
    }
    mavenJavadoc(MavenPublication) {
      artifact(javadocJar) { classifier "javadoc" }
    }
  }
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')
  publications = ['mavenJava']
  pkg {
    repo = project.group
    name = project.name
    userOrg = 'vaccovecrana'
    licenses = ['Apache-2.0']
    websiteUrl = website
    issueTrackerUrl = 'https://github.com/vaccovecrana/myrmica/issues'
    vcsUrl = 'https://github.com/vaccovecrana/myrmica.git'
    labels = ['maven', 'dependency', 'resolver']
    publicDownloadNumbers = true
    version {
      name = project.version
      desc = description
      gpg { sign = true }
    }
  }
}

wrapper { gradleVersion = "5.1.1" }
